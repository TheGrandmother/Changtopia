module main
-- Lets try to write the basics for a shell like thing!

def load_module(arg)
  match arg
    [name] ->
      match bif:request(bif:io_pid(), $load_module, name)
        $module_not_found ->
          io:print_strings(['Could not find module: ', name, '\n'])
          return $fail
        end
        $already_loaded ->
          io:print_strings([name, ' has already been loaded \n'])
          return $ok
        end
        whatever ->
          io:print_strings(['Loaded module: ', name, '\n'])
          return $ok
        end
      end
    end
    [] ->
      io:print_string('No module name provided\n')
      return $fail
    end
    whatever ->
      io:print_string('Load only takes the name of the module as an argument\n')
      return $fail
    end
  end
  return $ok
end

def run_module(arg)
  match arg
    [name] ->
      --return bif:run(name, '_entry')
      return spawn_module(name)
    end
    [] ->
      io:print_string('No module name provided\n')
      return $fail
    end
    whatever ->
      io:print_string('Run only takes the name of the module as an argument\n')
      return $fail
    end
  end
  return $ok
end

def help(_)
  io:print_string('Welcome to the ridicously pointless "operating system"\n')
  io:print_string('This is what you can do, which ain\'t much\n')
  return _help(commands())
end

def _help(commands)
  match commands
    [] ->
      return $ok
    end
    [entry, <rest>] ->
      [name, arg_count, _, help_text] = entry
      io:print_strings(['\t', name, ': ', help_text, '\n'])
      return _help(rest)
    end
  end
  return $ok
end

def spawn_module(name)
  pid = bif:spawn('main', '_spawn_module', name)
  bif:link(pid)
  response = bif:request(pid, $run)
  match response
    [$error, error_atom, msg] ->
      io:print_strings(['Error running: ', name, ':\n  ', msg, '\n'])
      return $error
    end
    $ok ->
      return $ok
    end
    [the_fuck, something, msg] ->
      return $ok
    end
    whatever ->
      io:print_strings([name, ' Sent us some strange shit...\n', msg, '\n'])
      return $ok
    end
  end
  bif:unlink(pid)
  return $ok
end

def _spawn_module(name)
  return bif:listen('main', '_spawn_listener', name)
end

def _spawn_listener(name, sender, payload)
  match payload
    $run ->
      return bif:run(name, '_entry')
    end
    whatever ->
      return $unknown_message
    end
  end
  return $ok
end

def dump(args)
  match args
    [name] ->
      [_, filePid] = bif:request(bif:io_pid(), $open_file, name)
      content = bif:request(filePid, $read)
      io:print_string(content)
      return $ok
    end
    whatever ->
      return $no_name
    end
  end
  return $ok
end

def quit(_)
  io:print_string('Thanks for nothing... cya l8r\n')
  bif:send(bif:io_pid(), $shut_down)
  return $ok
end

def ls(_)
  files = bif:request(bif:io_pid(), $list_files)
  io:print_string(list:join_with(files, '\n'))
  return $ok
end


def run_command(input)
  [name, <arguments>] = input
  match find_command(name)
    $not_found ->
      io:print_strings(['What is even "', name, '"?\n'])
      return $ok
    end
    [_, arg_count, thing, __] ->
      [module, function] = thing
      return bif:run(module, function, arguments)
    end
  end
  return $ok
end

def commands()
  -- Commands ar an array on the form:
  -- name, noArgs, [module, function], help_text
  bob = [
    ['q', 0, ['main', 'quit'], 'Quits the VM'],
    ['run', 1, ['main', 'run_module'], 'Runs the specified module'],
    ['load', 1, ['main', 'load_module'], 'Loads the specified module'],
    ['ls', 0, ['main', 'ls'], 'Lists existing files'],
    ['help', 0, ['main', 'help'], 'Prints this.. duh'],
    ['dump', 1, ['main', 'dump'], 'Dumps the content of the file']
  ]
  return bob
end

def find_command(name)
  bob = commands()
  return _find_command(name, bob)
end

def _find_command(name, commands)
  match commands
    [] ->
      return $not_found
    end
    [command, <rest>] ->
      [command_name, <_>] = command
      if bif:array_compare(name, command_name)
        return command
      end
      return _find_command(name, rest)
    end
  end
  return $ok
end

def prompt()
  bif:request(bif:io_pid(), $get_input_stream)
  input_string = io:get_line()
  io:print_string('\n')
  match run_command(list:split_by(input_string, " "))
    $quit ->
      return $quit
    end
    whatever ->
      return prompt()
    end
  end
  return $ok
end

def _entry()
  bif:send(bif:io_pid(), $print_string, 'Starting the Shell.\nLoading Modules:\n')
  match bif:request(bif:io_pid(), $load_module, 'io')
    $module_not_found ->
      bif:send(bif:io_pid(), $print_string, 'Could not load io module\n')
      quit([])
    end
  end
  match bif:request(bif:io_pid(), $load_module, 'list')
    $module_not_found ->
      bif:send(bif:io_pid(), $print_string, 'Could not load list module\n')
      quit([])
    end
  end
  match bif:request(bif:io_pid(), $load_module, 'utils')
    $module_not_found ->
      bif:send(bif:io_pid(), $print_string, 'Could not load utils module\n')
      quit([])
    end
  end
  prompt()
  return $ok
end
